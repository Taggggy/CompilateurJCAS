// ---------------------------------------------------------------------------
// syntaxe.cup
// Fichier cup qui définit l'analyseur syntaxique
// ---------------------------------------------------------------------------

package fr.esisar.compilation.syntaxe;

import java_cup.runtime.*;
import fr.esisar.compilation.global.src.*;
import java.io.InputStream;

/* Code pour l'analyseur syntaxique. */
parser code {:

   /**
    * Réalise l'analyse syntaxique du fichier passé en argument (ou de 
    * l'entrée standard si args est un tableau vide) et retourne 
    * l'abstrait correspondant.
    * Lève l'exception ErreurLexicale en cas d'erreur lexicale, et 
    * ErreurSyntaxe en cas d'erreur de syntaxe.
    */
   public static Arbre analyseSyntaxique(String[] args) 
         throws Exception, ErreurLexicale, ErreurSyntaxe {

      // On récupère le fichier à analyser
      InputStream fichierCas = ArgsFichier.ouvrir(args);

      // Création de l'analyseur lexical
      Lexical analyseurLex = new Lexical(fichierCas);

      // Création de l'analyseur syntaxique
      parser analyseurSynt = new parser(analyseurLex);

      // Appel de l'analyseur syntaxique et récupération de l'arbre résultat
      Object result = analyseurSynt.parse().value;
      Arbre arbre = (Arbre) result;
      
      // On retourne l'arbre abstrait construit
      return arbre;
   }

   /**
    * Affiche un message d'erreur en cas d'erreur de syntaxe.
    */
   public void syntax_error(Symbol cur_token) {
      // Affichage du message d'erreur, avec le numéro de ligne 
      System.out.println("Erreur de syntaxe ... ligne " + numLigne()) ;
   }

   /**
    * Méthode appelée en cas d'erreur de syntaxe.
    */
   public void unrecovered_syntax_error(Symbol cur_token)
         throws ErreurSyntaxe {
      throw new ErreurSyntaxe();
   }

   /**
    * Retourne le numéro de ligne courant.
    */
   public int numLigne() {
      // On récupère l'analyseur lexical
      Lexical anaLex = (Lexical) getScanner();
      return anaLex.numLigne();
   }
:};

/* Terminaux (tokens retournés par l'analyseur lexical. */
terminal String IDF;          // Identificateur
terminal Integer CONST_ENT;   // Constante entière
terminal Float CONST_REEL;    // Constante réelle
terminal String CONST_CHAINE; // Constante chaîne

/* Mots réservés */
terminal   AND,         ARRAY,       BEGIN;
terminal   DIV,         DO,          DOWNTO;
terminal   ELSE,        END,         FOR;
terminal   IF,          MOD,         NEW_LINE;
terminal   NOT,         NULL,        OF;
terminal   OR,          PROGRAM,     READ;
terminal   THEN,        TO,          WHILE;
terminal   WRITE;

/* Opérateurs */
terminal INF, SUP, EGAL, DIFF, // "<", ">", "=", "/="
         INF_EGAL, SUP_EGAL,   // "<=", ">="
         PLUS, MOINS,          // "+", "-"
         MULT, DIV_REEL;       // "*", "/"

/* Autres terminaux */
terminal PAR_OUVR, PAR_FERM; // "(", ")" 
terminal DOUBLE_POINT;       // ".."
terminal DEUX_POINTS;        // ":"
terminal VIRGULE;            // ","
terminal POINT_VIRGULE;      // ";"
terminal CROCH_OUVR;         // "["
terminal CROCH_FERM;         // "]"
terminal AFFECT;             // ":="
terminal POINT;              // "."

/* Définition des non terminaux de la grammaire */
non terminal Arbre program;
non terminal Arbre liste_decl;
non terminal Arbre liste_inst;
non terminal Arbre inst;
non terminal Arbre decl;
non terminal Arbre liste_idf;
non terminal Arbre type;
non terminal Arbre idf;
non terminal Arbre type_intervalle;
non terminal Arbre constante;
non terminal Arbre const;
non terminal Arbre pas;
non terminal Arbre liste_exp;
non terminal Arbre exp;
non terminal Arbre facteur;
non terminal Arbre place;

/*
   On définit les priorités des opérateurs
   Les priorités vont dans l'ordre croissant
   On déclare egalement l'associativité des opérateurs
   (left, right, ou nonassoc)
*/

precedence nonassoc INF, SUP, EGAL, DIFF, INF_EGAL, SUP_EGAL;
precedence left PLUS, MOINS, OR;
precedence left MULT, DIV_REEL, DIV, MOD, AND;
precedence nonassoc NOT;

program ::= PROGRAM:p liste_decl:a1 BEGIN liste_inst:a2 END POINT
   {:
      RESULT = Arbre.creation2(Noeud.Programme, a1, a2, pleft);
      // pleft : Numéro de ligne du mot réservé "program".
   :}
   ;
liste_decl ::= liste_decl:a1 decl:a2 POINT_VIRGULE
   {:
      RESULT = Arbre.creation2(Noeud.ListeDecl, a1, a2, a2.getNumLigne());
   :}
   |  // epsilon
   {:
      RESULT = Arbre.creation0(Noeud.Vide, parser.numLigne());
   :}
   ;

// ---------------------------------------------
// Actions ajoutées ci-dessous
// ---------------------------------------------



decl ::= liste_idf:a1 DEUX_POINTS type:a2
   {:
      RESULT = Arbre.creation2(Noeud.Decl, a1, a2, parser.numLigne());
   :}
   ;
liste_idf ::= liste_idf:a1 VIRGULE idf:a2
   {: 
      RESULT = Arbre.creation2(Noeud.ListeIdent, a1, a2, parser.numLigne());
   :}
   |  idf:a
   {:
      RESULT = Arbre.creation2(Noeud.ListeIdent, Arbre.creation0(Noeud.Vide, parser.numLigne()), a, aleft);
   :}
   ;

type ::= idf:a
   {:
      RESULT = a;
   :}
   |  type_intervalle:a
   {:
      RESULT = a;
   :}
   |  ARRAY CROCH_OUVR type_intervalle:a1 CROCH_FERM OF type:a2
   {:
      RESULT = Arbre.creation2(Noeud.Tableau, a1, a2, parser.numLigne());
   :}
   ;

type_intervalle ::= constante:a1 DOUBLE_POINT constante:a2
   {:
      RESULT = Arbre.creation2(Noeud.Intervalle, a1, a2, parser.numLigne());
   :}
   ;

constante ::= PLUS const:a
   {:
      RESULT = Arbre.creation1(Noeud.PlusUnaire, a, aleft);
   :}
   |  MOINS const:a
   {: 
      RESULT = Arbre.creation1(Noeud.MoinsUnaire, a, aleft);
   :}
   |  const:a
   {:
      RESULT = a;
   :}
   ;

const ::= CONST_ENT:a
   {:
      RESULT = Arbre.creationEntier(a, aleft);
   :}
   |  idf:a
   {:
      RESULT = a;
   :}
   ;
idf ::= IDF:a
   {:
      RESULT = Arbre.creationIdent(a, aleft);
   :}
   ;

liste_inst ::= liste_inst:a1 inst:a2 POINT_VIRGULE
   {: 
      RESULT = Arbre.creation2(Noeud.ListeInst, a1, a2, a2.getNumLigne());
   :}
   |  inst:a POINT_VIRGULE
   {:
      RESULT = Arbre.creation2(Noeud.ListeInst, Arbre.creation0(Noeud.Vide, parser.numLigne()), a, aleft);
   :}
   ;

inst ::= NULL
   {:
	  RESULT = Arbre.creation0(Noeud.Nop, parser.numLigne());
   :}
   |  place:p AFFECT exp:e
   {:
      RESULT = Arbre.creation2(Noeud.Affect, p, e, parser.numLigne());
   :}
   |  FOR:f pas:p DO liste_inst:l END
   {:
      RESULT = Arbre.creation2(Noeud.Pour, p, l, fleft);
   :}
   |  WHILE:w exp:e DO liste_inst:l END
   {:
      RESULT = Arbre.creation2(Noeud.TantQue, e, l, wleft);
   :}
   |  IF:i exp:e THEN liste_inst:l END
   {:
      RESULT = Arbre.creation3(Noeud.Si, e, l, Arbre.creation0(Noeud.Vide, parser.numLigne()), ileft);
   :}
   |  IF:i exp:e THEN liste_inst:l1 ELSE liste_inst:l2 END
   {:
      RESULT = Arbre.creation3(Noeud.Si, e, l1, l2, ileft);
   :}
   |  WRITE PAR_OUVR liste_exp:l PAR_FERM
   {:
      RESULT = Arbre.creation1(Noeud.Ecriture, l, lleft);
   :}
   |  READ PAR_OUVR place:p PAR_FERM
   {:
      RESULT = Arbre.creation1(Noeud.Lecture, p, pleft);
   :}
   |  NEW_LINE
   {:
      RESULT = Arbre.creation0(Noeud.Ligne, parser.numLigne());
   :}
   ;

pas ::= idf:i AFFECT exp:e1 TO exp:e2
   {:
      RESULT = Arbre.creation3(Noeud.Increment, i, e1, e2, parser.numLigne());
   :}
   |  idf:i AFFECT exp:e1 DOWNTO exp:e2
   {: 
      RESULT = Arbre.creation3(Noeud.Decrement, i, e1, e2, parser.numLigne());
   :}
   ;

liste_exp ::= liste_exp:l VIRGULE exp:e
   {:
      RESULT = Arbre.creation2(Noeud.ListeExp, l, e, parser.numLigne());
   :}
   |  exp:e
   {:
   	  RESULT = Arbre.creation2(Noeud.ListeExp, Arbre.creation0(Noeud.Vide, parser.numLigne()), e, eleft);
   :}
   ;
   
exp ::= facteur:a
   {:
      RESULT = a;
   :}
   |  exp:e1 AND exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Et,e1,e2,parser.numLigne());
   :}
   |  exp:e1 OR exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Ou,e1,e2,parser.numLigne());
   :}
   |  exp:e1 EGAL exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Egal,e1,e2,parser.numLigne());
   :}
   |  exp:e1 INF_EGAL exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.InfEgal,e1,e2,parser.numLigne());
   :}
   |  exp:e1 SUP_EGAL exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.SupEgal,e1,e2,parser.numLigne());
   :}
   |  exp:e1 DIFF exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.NonEgal,e1,e2,parser.numLigne());
   :}
   |  exp:e1 INF exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Inf,e1,e2,parser.numLigne());
   :}
   |  exp:e1 SUP exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Sup,e1,e2,parser.numLigne());
   :}
   |  exp:e1 PLUS exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Plus,e1,e2,parser.numLigne());
   :}
   |  exp:e1 MOINS exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Moins,e1,e2,parser.numLigne());
   :}
   |  exp:e1 MULT exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Mult,e1,e2,parser.numLigne());
   :}
   |  exp:e1 DIV_REEL exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.DivReel,e1,e2,parser.numLigne());
   :}
   |  exp:e1 MOD exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Reste,e1,e2,parser.numLigne());
   :}
   |  exp:e1 DIV exp:e2
   {:
      RESULT = Arbre.creation2(Noeud.Quotient,e1,e2,parser.numLigne());
   :}
   |  PLUS facteur:f
   {: 
      RESULT = Arbre.creation1(Noeud.PlusUnaire,f,fleft);
   :}
   |  MOINS facteur:f
   {: 
      RESULT = Arbre.creation1(Noeud.MoinsUnaire,f,fleft);
   :}
   |  NOT facteur:f
   {: 
      RESULT = Arbre.creation1(Noeud.Non,f,fleft);
   :}
   ;

facteur ::= CONST_ENT:c
   {:
      RESULT = Arbre.creationEntier(c,cleft);
   :}
   |  CONST_REEL:c
   {: 
      RESULT = Arbre.creationReel(c,cleft);
   :}
   |  CONST_CHAINE:c
   {:
      RESULT = Arbre.creationChaine(c,cleft);
   :}
   |  place:p
   {:
      RESULT = p;
   :}
   |  PAR_OUVR exp:e PAR_FERM
   {:
      RESULT = e;
   :}
   ;

place ::= idf:a
   {:
      RESULT = a;
   :}
   |  place:p CROCH_OUVR exp:e CROCH_FERM
   {:
      RESULT = Arbre.creation2(Noeud.Index, p, e, parser.numLigne());
   :}
   ;
